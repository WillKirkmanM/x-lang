COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" | COMMENT }

program = { SOI ~ statement* ~ EOI }
statement = { function_def | (var_decl | import | expr) ~ ";" }

function_def = { "fn" ~ identifier ~ "(" ~ params? ~ ")" ~ block }
params = { identifier ~ ("," ~ identifier)* }
block = { "{" ~ statement* ~ "}" }

import = { "import" ~ module ~ "::" ~ item }
module = { identifier }
item = { identifier }

var_decl = { "let" ~ identifier ~ "=" ~ expr }

expr = { term ~ ((operator ~ term)*)? }
operator = _{ add | subtract | multiply | divide }

term = { function_call | number | string | identifier }

function_call = { identifier ~ "(" ~ args? ~ ")" }
args = { expr ~ ("," ~ expr)* }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ ASCII_DIGIT+ }

add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }